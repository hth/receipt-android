import groovy.xml.Namespace

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.google.gms:google-services:1.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.google.gms.google-services'

apply from: 'config/quality.gradle'

if (!hasProperty('env')) {
    env = System.env['GRADLE_ENV']
}

def buildNumber = System.getenv("BUILD_NUMBER") ?: "0"

def manifestVersionName() {
    def manifestFile = file(project.projectDir.absolutePath + '/src/main/AndroidManifest.xml')
    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)
    return xml.attributes()[ns.versionName].toString()
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-milestone" }
    maven { url "http://repo1.maven.org/maven2/" }
    maven { url "https://jitpack.io" }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    /** Pretty messages. */
    /** Android library for material design. */
    // compile 'com.github.navasmdc:MaterialDesign:1.3'
    /** Pull refresh. */
    /** Material Navigation Drawer */
    /** Super toast */
    /** Image download. */
    /** Icons. */
    /** Text Drawable. */
    /** Word Utils */
    /** Swipe Menu. */
    //    compile 'com.android.support:recyclerview-v7:23.1.1'
    //    compile 'com.android.support:support-v4:23.1.1'
    //    compile "com.daimajia.swipelayout:library:1.2.0@aar"
    // compile 'com.aurelhubert:ahbottomnavigation:2.0.1'
    compile 'com.r0adkll:postoffice:1.1.9'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'it.neokree:MaterialNavigationDrawer:1.3.3'
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.joanzapata.iconify:android-iconify-fontawesome:2.1.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.baoyz.swipemenulistview:library:1.3.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile 'net.danlew:android.joda:2.9.1'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.0.Final'
    compile 'com.braintreepayments.api:braintree:1.6.5'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.facebook.android:facebook-android-sdk:3.23.1'
    compile 'com.google.guava:guava:19.0'
    compile 'ch.acra:acra:4.7.0'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.jayway.android.robotium:robotium-solo:5.4.1'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'

}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

android {
    archivesBaseName = "receiptApp";

    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    def versionPropsFile = file('version.properties')
    def versionMajor = 0;
    def versionMinor = 0;
    def versionPatch = 0;

    if (versionPropsFile.canRead()) {
        if (env.equals("release") || env.equals("staging")) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionMajor = versionProps['VERSION_MAJOR'].toInteger()
            versionMinor = versionProps['VERSION_MINOR'].toInteger()
            versionPatch = versionProps['VERSION_PATCH'].toInteger()
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    def versionBuildPropsFile = file('version.build.properties')
    def versionBuild = 0;

    if (env.equals("release") || env.equals("staging")) {
        if (versionBuildPropsFile.canRead()) {
            def Properties versionBuildProps = new Properties()
            versionBuildProps.load(new FileInputStream(versionBuildPropsFile))

            if (versionBuildProps['VERSION_MAJOR'].toInteger() != versionMajor ||
                    versionBuildProps['VERSION_MINOR'].toInteger() != versionMinor) {

                println "versionPatch: " + 0.toString() +
                        " versionBuild: " + versionBuildProps['VERSION_BUILD']
                versionBuild = versionBuildProps['VERSION_BUILD'].toInteger() + 1
                versionBuildProps['VERSION_BUILD'] = versionBuild.toString()

                versionBuildProps['VERSION_MAJOR'] = versionMajor.toString()
                versionBuildProps['VERSION_MINOR'] = versionMinor.toString()
                versionBuildProps['VERSION_PATCH'] = versionPatch.toString()
                versionBuildProps.store(versionBuildPropsFile.newWriter(), null)
            } else {
                println "versionBuild: " + versionBuildProps['VERSION_BUILD'] +
                        " versionBuild: " + versionBuildProps['VERSION_BUILD']
                versionBuild = versionBuildProps['VERSION_BUILD'].toInteger() + 1
                versionBuildProps['VERSION_BUILD'] = versionBuild.toString()

                versionBuildProps['VERSION_MAJOR'] = versionMajor.toString()
                versionBuildProps['VERSION_MINOR'] = versionMinor.toString()

                versionPatch = versionBuildProps['VERSION_PATCH'].toInteger() + 1
                versionBuildProps['VERSION_PATCH'] = versionPatch.toString()
                versionBuildProps.store(versionBuildPropsFile.newWriter(), null)
            }
        } else {
            throw new GradleException("Could not read version.build.properties!")
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    defaultConfig {
        applicationId "com.receiptofi.receiptapp"
        minSdkVersion 16
        targetSdkVersion 24
        multiDexEnabled true
        testHandleProfiling true
        testFunctionalTest true
        versionCode versionBuild
        versionName versionMajor + "." + versionMinor + "." + versionPatch

        // For now we enable strict mode for all the configs
        buildConfigField 'boolean', 'STRICT_MODE', 'true'
        buildConfigField "String", "BUILD", "\"$buildNumber\""

        if (env == 'debug' || env == 'release' || env == 'staging') {
            apkName = "-" + versionMajor + "." + versionMinor + "." + versionPatch + "." + versionBuild + "-" + buildNumber + ".apk"
        }

        ext.versionName = manifestVersionName()
        println "Building Receiptofi ReceiptApp for " +
                "environment: $env " +
                "app-version: $versionName " +
                "apkName ends with : $apkName"

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File(
                        output.outputFile.parent,
                        output.outputFile.name.replace(".apk", "$apkName")
                )
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        staging
        release
    }
    apply from: 'receipt-android-app-signing-files/signing.gradle'

    buildTypes {
        def BOOLEAN = "boolean"
        def STRING = "String"
        def TRUE = "true"
        def FALSE = "false"
        def LOG_HTTP_REQUESTS = "LOG_HTTP_REQUESTS"
        def REPORT_CRASHES = "REPORT_CRASHES"
        def ENABLE_VIEW_SERVER = "ENABLE_VIEW_SERVER"
        def ENABLE_SHARING = "ENABLE_SHARING"
        def DEBUG_IMAGES = "DEBUG_IMAGES"

        debug {
            debuggable true
            minifyEnabled true

            proguardFile 'library-proguard/proguard-crashlytics.pro'
            proguardFile 'library-proguard/proguard-facebook.pro'
            proguardFile 'library-proguard/proguard-facebook-conceal.pro'
            proguardFile 'library-proguard/proguard-google-analytics.pro'
            proguardFile 'library-proguard/proguard-google-play-services.pro'
            proguardFile 'library-proguard/proguard-gson.pro'
            proguardFile 'library-proguard/proguard-mikephil-charting.pro'
            proguardFile 'library-proguard/proguard-org-apache.pro'

            proguardFile 'receiptofi-debug-proguard.config'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, TRUE
            buildConfigField BOOLEAN, ENABLE_SHARING, TRUE
            buildConfigField BOOLEAN, DEBUG_IMAGES, TRUE

            buildConfigField STRING, "PROVIDER_AUTHORITY", debugProviderAuthority
            buildConfigField STRING, "RECEIPTOFI_MOBILE", debugEndpoint
            buildConfigField STRING, "GOOGLE_CLIENT_ID", googleClientIdLocal
            buildConfigField STRING, "AWSS3", debugEndpointAwsS3
            buildConfigField STRING, "AWSS3_BUCKET", debugBucket
            resValue "string", "FACEBOOK_APP_ID", facebookAppIdLocal
        }

        staging {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true

            proguardFile 'library-proguard/proguard-crashlytics.pro'
            proguardFile 'library-proguard/proguard-facebook.pro'
            proguardFile 'library-proguard/proguard-facebook-conceal.pro'
            proguardFile 'library-proguard/proguard-google-analytics.pro'
            proguardFile 'library-proguard/proguard-google-play-services.pro'
            proguardFile 'library-proguard/proguard-gson.pro'
            proguardFile 'library-proguard/proguard-mikephil-charting.pro'
            proguardFile 'library-proguard/proguard-org-apache.pro'

            proguardFile 'receiptofi-staging-proguard.config'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.staging

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE

            buildConfigField STRING, "PROVIDER_AUTHORITY", stagingProviderAuthority
            buildConfigField STRING, "RECEIPTOFI_MOBILE", stagingEndpoint
            buildConfigField STRING, "GOOGLE_CLIENT_ID", googleClientIdStaging
            buildConfigField STRING, "AWSS3", stagingEndpointAwsS3
            buildConfigField STRING, "AWSS3_BUCKET", stagingBucket
            resValue "string", "FACEBOOK_APP_ID", facebookAppIdStaging
        }

        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true

            proguardFile 'library-proguard/proguard-crashlytics.pro'
            proguardFile 'library-proguard/proguard-facebook.pro'
            proguardFile 'library-proguard/proguard-facebook-conceal.pro'
            proguardFile 'library-proguard/proguard-google-analytics.pro'
            proguardFile 'library-proguard/proguard-google-play-services.pro'
            proguardFile 'library-proguard/proguard-gson.pro'
            proguardFile 'library-proguard/proguard-mikephil-charting.pro'
            proguardFile 'library-proguard/proguard-org-apache.pro'

            proguardFile 'receiptofi-release-proguard.config'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            signingConfig signingConfigs.release

            buildConfigField BOOLEAN, LOG_HTTP_REQUESTS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, ENABLE_VIEW_SERVER, FALSE
            buildConfigField BOOLEAN, ENABLE_SHARING, FALSE
            buildConfigField BOOLEAN, DEBUG_IMAGES, FALSE

            buildConfigField STRING, "PROVIDER_AUTHORITY", releaseProviderAuthority
            buildConfigField STRING, "RECEIPTOFI_MOBILE", releaseEndpoint
            buildConfigField STRING, "GOOGLE_CLIENT_ID", googleClientIdLive
            buildConfigField STRING, "AWSS3", releaseEndpointAwsS3
            buildConfigField STRING, "AWSS3_BUCKET", releaseBucket
            resValue "string", "FACEBOOK_APP_ID", facebookAppIdLive
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors true
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport true
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("lint-report.html")

        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // Set the severity of the given issues to fatal (which means they will be
        // checked during release builds (even if the lint target is not included)
        fatal 'NewApi', 'InlineApi'
        // Set the severity of the given issues to error
        error 'Wakelock', 'TextViewEdits'
        // Set the severity of the given issues to warning
        warning 'ResourceAsColor'
        // Set the severity of the given issues to ignore (same as disabling the check)
        ignore 'TypographyQuotes'
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['androidTest/java']
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

/** Gradle default task. */
defaultTasks 'clean', 'assembleDebug'